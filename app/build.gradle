plugins {
    id 'com.android.application'
}

// 当前beta版本号
ext.build_number = "1.0";

// 显示的时间格式，形如20171110
static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault());
}

def loadSigningConfigs() {
    def Properties props = new Properties()
    def propFile = file('./signing.conf')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('RELEASE_STORE_FILE') && props.containsKey('RELEASE_STORE_PASSWORD') &&
                props.containsKey('RELEASE_KEY_ALIAS') && props.containsKey('RELEASE_KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['RELEASE_STORE_FILE'])
            android.signingConfigs.release.storePassword = props['RELEASE_STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['RELEASE_KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['RELEASE_KEY_PASSWORD']
            android.signingConfigs.debug.storeFile = file(props['RELEASE_STORE_FILE'])
            android.signingConfigs.debug.storePassword = props['RELEASE_STORE_PASSWORD']
            android.signingConfigs.debug.keyAlias = props['RELEASE_KEY_ALIAS']
            android.signingConfigs.debug.keyPassword = props['RELEASE_KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
            android.buildTypes.debug.signingConfig = null
        }
    } else {
        android.buildTypes.release.signingConfig = null
        android.buildTypes.debug.signingConfig = null
    }
}

android {

    applicationVariants.all { variant ->
        variant.outputs.all  { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 显示app名称+当前版本号
                def fileName = "EmojiEdit-V${variant.versionName}"
                // 如果有渠道，显示渠道名""
                if (variant.flavorName!=""){
                    fileName = fileName + "-${variant.flavorName}"
                }
                // 如果是debug版本，加入beta版本号
                if ('debug'.equals(variant.buildType.name)) {
                    fileName = fileName + "-${build_number}.apk"
                } else {
                    // 如果是release版本，直接显示
                    fileName = fileName + "-release.apk"
                }
                outputFileName   = fileName
            }
        }
    }

    signingConfigs {
        debug {
//            storeFile file('E:\\projects\\android\\signature\\woolensheep.jks')
//            storePassword 'ws@3---223'
//            keyAlias 'key0'
//            keyPassword 'ws@3---223'
        }
        release {
//            storeFile file('E:\\projects\\android\\signature\\woolensheep.jks')
//            storePassword 'ws@3---223'
//            keyPassword 'ws@3---223'
//            keyAlias 'key0'
        }
    }
    loadSigningConfigs()
    compileSdkVersion 30

    defaultConfig {
        applicationId "top.woolensheep.emojiedit"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            // 不显示log
//            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
//            zipAlignEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    externalNativeBuild {
        cmake {
            version '3.10.2'
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs','src/main/java']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.75'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

}